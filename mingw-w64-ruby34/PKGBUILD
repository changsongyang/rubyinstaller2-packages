_realname=ruby
pkgbase=mingw-w64-ruby34
pkgname="${MINGW_PACKAGE_PREFIX}-ruby34"
# version used by MRI
pkgver2=3.4.1
# version used by pacman
pkgver="${pkgver2/-/.}"
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'ucrt64' 'clangarm64')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             autoconf automake libtool patch make)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=3.0.0"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver2%.*}/${_realname}-${pkgver2}.tar.xz"
        0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
        0002-Don-t-add-an-exe-file-manifest.patch
        ruby.ico
        rubyw.ico)

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD"

sha256sums=('018d59ffb52be3c0a6d847e22d3fd7a2c52d0ddfee249d3517a0c8c6dbfa70af'
            'a50c81ab8c178689485257f8eaa70bd8184b878a798780084668f838912e2832'
            'da079dbfdc3d4e1b976ed15d58244ed6e0df201ec829bead578fb4a7177b9cab'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver2}
  rm -rf ext/win32/dll_directory
  patch -p1 -i ${srcdir}/0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
  patch -p1 -i ${srcdir}/0002-Don-t-add-an-exe-file-manifest.patch
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  CFLAGS="-O3 -fstack-protector-strong"
  LDFLAGS="-pipe -s -fstack-protector-strong"
  if [[ ${MSYSTEM} == CLANG* ]]; then
    CFLAGS+=" -Wno-dll-attribute-on-redeclaration -Wno-shorten-64-to-32"
  else
    CFLAGS+=" -Wno-incompatible-pointer-types"
  fi

  cp ${srcdir}/*.ico .

  ../${_realname}-${pkgver2}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog

  make showflags main
  echo "Generate RI and HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make rdoc html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd build-${MSYSTEM}
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd build-${MSYSTEM}
  make -f GNUMakefile DESTDIR="${pkgdir}" install
  for script in gem; do
    install ${srcdir}/ruby-${pkgver2}/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done
}
